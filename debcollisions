#!/usr/bin/perl

# Copyright © 2014 Jakub Wilk <jwilk@debian.org>
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED “AS IS” AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
# REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
# AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
# INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
# LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use v5.10;

if (scalar @ARGV < 1) {
    say STDERR "usage: $0 <deb>...";
    exit 1;
}

my @PATH = qw(/bin /usr/bin /sbin /usr/sbin /usr/games);
my $PATH = join('|', @PATH);
$PATH = qr($PATH);

my %files = ();
for my $deb (@ARGV) {
    open my $fh, '-|', 'dpkg', '-c', $deb or die;
    while (<$fh>) {
        m{[.](/.*)$} or die "$0: cannot parse $_";
        my $file = $1;
        next if $file eq '/';
        if ($file =~ m{^$PATH/(.+)}) {
            my $rest = $1;
            for my $dir (@PATH) {
                $files{"$dir/$rest"} = 1;
            }
        } else {
            $file =~ s{/$}{};
            $files{$file} = 1;
        }
    }
    close $fh or die;
}
my @files = sort keys %files;

open my $fh, '|-', 'apt-file', 'search', '-F', '-f', '-' or die;
for my $file (@files) {
    say {$fh} $file;
}
close $fh or die;

# vim:ts=4 sw=4 et
