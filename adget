#!/usr/bin/perl
#
# Copyright Â© 2022 Jakub Wilk <jwilk@jwilk.net>
# SPDX-License-Identifier: MIT

### APT-powered dget(1) ###

use strict;
use warnings;

BEGIN {
    *CORE::GLOBAL::system = sub {
        my @args = @_;
        if ("@args" =~ m/^command -v curl /) {
            return 1;
        }
        if ("@args" =~ m/^command -v wget /) {
            return 0;
        }
        if (@args >= 4 and $args[0] eq 'wget' and $args[-3] eq '-O') {
            my ($file, $url) = splice @args, -2, 2;
            @args = (qw(/usr/lib/apt/apt-helper download-file), $url, $file);
        }
        if (@args >= 1 and ($args[0] eq 'wget' or $args[0] eq 'curl')) {
            die "system(): unexpected command: @args";
        }
        return CORE::system @args;
    };
}

do '/usr/bin/dget';
if ($@) {
    die $@;
}

# vim:ts=4 sts=4 sw=4 et
